''' Description  :
'''      this code sets LEDA as link status display using standard SPITRANSER
'''      and makes LEDB blink permamently
''' target devices :
'''      any PIC with integrated SPI on PORTC
'''      the ENC28J60 SPI plus CLK, SO, SI must be connected to the corresponding SPI pins of the PIC
'''      the INT and WOL signals from the ENC are not used

''' If LEDA is off and LEDB starts blinking, that is good news : the PIC can talk to the ENC controller through SPI communication.
''' If it does not, it should, check again your solderings & connections until it works.
'''
'''  Then connect your board to a hub with a straight cable : LEDA should now turn on with LEDB still blinking. LEDA on means that the adapter is correctly linked to the network. The network link LED of the other side hub should also turn on.
	#chip 16f18855,32
	#Config FEXTOSC_OFF, RSTOSC_HFINT32
	#Config WRT_OFF, CPD_ON, MCLRE_ON

    'Set the PPS of the ports.
    UNLOCKPPS
      'USART
      RC0PPS = 0x0010		  'RC0->EUSART:TX;
      RXPPS  = 0x0011 		'RC1->EUSART:RX;

      'SPI
      RB3PPS = 0x0014     'RB3->MSSP1:SCK1;
      SSP1CLKPPS = 0x000B 'RB3->MSSP1:SCK1;
      SSP1DATPPS = 0x000C 'RB4->MSSP1:SDI1;
      RB5PPS = 0x0015     'RB5->MSSP1:SDO1;
    LOCKPPS

    'USART settings
    #define USART_BAUD_RATE 9600
    #define USART_BLOCKING
    #define USART_TX_BLOCKING

; ----- Define Hardware settings
    ; ----- Constants
    #define ENC28J60_DI			portb.4						'Connected to Device SO
    #define ENC28J60_DO			portb.5							'Connected to Device SI
    #define ENC28J60_SCK		portb.3							'Connected to Clock
    #define ENC28J60_CS 		portb.2							'Chip select line
    #define ENC28J60_RESET  PORTb.1							'Reset line

        wait 1 s
        HSerPrintCRLF 2
        HserPrint "ENC28J60 Ethernet Driver - Flash LED Test" : HSerPrintCRLF

        HserPrint "Init Test": HSerPrintCRLF


        #define ENC28J60_Select         Set ENC28J60_CS off
        #define ENC28J60_Deselect       Set ENC28J60_CS on
        #define ENC28J60_Reset_CMD			Set ENC28J60_RESET on: Wait 10 ms :Set ENC28J60_RESET off: Wait 10 ms :Set ENC28J60_RESET on: Wait 10 ms


        Dir ENC28J60_DI in
        Dir ENC28J60_DO Out
        Dir ENC28J60_SCK Out
        Dir ENC28J60_CS out
        Dir ENC28J60_RESET out

        'Initial state of ports required for SW SPI
        set ENC28J60_DO off
        set ENC28J60_SCK off
        set ENC28J60_CS off


        'Reset the NIC
        ENC28J60_Deselect
        SPIMode Master, 0
        ENC28J60_Reset_CMD

          ' set ENC memory bank 2

        ENC28J60_Select
        SPITransfer ( ( BFCCMD | ECON1 ), temp )
        SPITransfer(0b00000001, temp )
        ENC28J60_Deselect

        ENC28J60_Select
        SPITransfer( BFSCMD | ECON1, temp )
        SPITransfer(0b00000010 , temp )
        ENC28J60_Deselect

        'Set address of PHY register to write
        ENC28J60_writeReg(MIREGADR, PHLCON)
        'Set LEDA for link status display
        'and LEDB for blink fast
        ENC28J60_writeReg(MIWRL, 0b10100010)
        ENC28J60_writeReg(MIWRH, 0b00000100)



        ; writes byte ENC_Outbyte in ENC register addr of the current bank
        sub ENC28J60_writeReg( in addr,  in ENC_Outbyte as byte )

                ENC28J60_Select
                SPITransfer( addr OR WCRCMD , temp )
                SPITransfer( ENC_Outbyte, temp )
                ENC28J60_Deselect

        end sub





' ENC SPI commands
#define WCRCMD  0b01000000      'write control register
#define BFCCMD  0b10100000      'bit field clear
#define BFSCMD  0b10000000      'bit field set
#define BFCCMD  0b10100000      'bit field clear


' ENC SFR
#define MIREGADR        0x14
#define ECON1           0x1f

#define PHLCON          0x14
#define MIWRL           0x16

'	ENC PHY register
#define MIWRH   0x17
